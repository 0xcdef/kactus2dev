# If empty, will be considered as an installation for all users, requiring admin-privileges.
LOCAL_INSTALL_DIR=""

isEmpty(LOCAL_INSTALL_DIR) {
    # Get the bitness of the system.
    UNAME = $$system(uname -m)

    # Select paths for binaries in accordance with convention.
    bin_path = /usr/bin
    # Lib path depend on the bitness of the system.
    equals(UNAME, x86_64): lib_path = /usr/lib64
    !equals(UNAME, x86_64): lib_path = /usr/lib
    plugin_path = /usr/share/kactus2/plugins

    # Files and destination path for possible settings file upgrades.
    unix:upgrade.path = /usr/share/kactus2
    unix:upgrade.files = ./Administrative/releaseFiles/upgrade.cfg

    # Files and destination path for documentation files other than the manual.
    unix:doc.path = /usr/share/kactus2/doc
    unix:doc.files = ./Administrative/releaseFiles/*.txt

    # Files and destination path for the manual.
    unix:help.path = /usr/share/kactus2/help
    unix:help.files = ./Help/Kactus2Help.qch ./Help/Kactus2Help.qhc

    # Location for the default setting files.
    unix:config.path = /etc/xdg/TUT

    # Copying stuff, exact locations seem to depend on the bitness of the system.
    equals(UNAME, x86_64): unix:config.extra = mkdir -p $(INSTALL_ROOT)/etc/xdg/TUT; cp ./Administrative/releaseFiles/DefaultSettingsLinux.ini $(INSTALL_ROOT)/etc/xdg/TUT/Kactus2.ini; (test -d $(INSTALL_ROOT)/usr/lib64 && ln -f -s $(INSTALL_ROOT)/usr/bin/kactus2 $(INSTALL_ROOT)/usr/lib64/libKactus2.so) || (test -d $(INSTALL_ROOT)/lib/x86_64-linux-gnu && ln -f -s $(INSTALL_ROOT)/usr/bin/kactus2 $(INSTALL_ROOT)/lib/x86_64-linux-gnu/libKactus2.so)
    !equals(UNAME, x86_64): unix:config.extra = mkdir -p $(INSTALL_ROOT)/etc/xdg/TUT; cp ./Administrative/releaseFiles/DefaultSettingsLinux.ini $(INSTALL_ROOT)/etc/xdg/TUT/Kactus2.ini; ln -f -s $(INSTALL_ROOT)/usr/bin/kactus2 $(INSTALL_ROOT)/usr/lib/libKactus2.so

    # Files and destination path for the IP-XACT files coming with an installation.
    unix:library.path = /usr/share/kactus2/library
    unix:library.files = ./Administrative/releaseFiles/Library/*

    # Files and destination path for the icons.
    unix:icons.path = /usr/share/pixmaps
    unix:icons.files = ./Administrative/releaseFiles/kactus2.xpm ./Administrative/releaseFiles/kactus2.png

    # Files and destination path for the desktop shortcut.
    unix:desktop.path = /usr/share/applications
    unix:desktop.files = ./Administrative/releaseFiles/kactus2.desktop
} else {
    # Binaries shall be located in the root of the target installation directory.
    bin_path = $$LOCAL_INSTALL_DIR
    lib_path = $$LOCAL_INSTALL_DIR
    plugin_path = $$LOCAL_INSTALL_DIR/Plugins

    # Files and destination path for possible settings file upgrades.
    unix:upgrade.path = $$LOCAL_INSTALL_DIR
    unix:upgrade.files = ./Administrative/releaseFiles/upgrade.cfg

    # Files and destination path for documentation files other than the manual.
    unix:doc.path = $$LOCAL_INSTALL_DIR
    unix:doc.files = ./Administrative/releaseFiles/*.txt

    # Files and destination path for the manual.
    unix:help.path = $$LOCAL_INSTALL_DIR/Help
    unix:help.files = ./Help/Kactus2Help.qch ./Help/Kactus2Help.qhc

    # Location for the default setting files.
    unix:config.path = /etc/xdg/TUT

    # Copy the default settings directly to the user's config directory. 
	# Uses the same default settings as in Windows.
    unix:config.extra = cp ./Administrative/releaseFiles/DefaultSettingsWin.ini /etc/xdg/TUT/Kactus2.ini

    # Files and destination path for the IP-XACT files coming with an installation.
    unix:library.path = $$LOCAL_INSTALL_DIR/Library
    unix:library.files = ./Administrative/releaseFiles/Library/*

    # Files and destination path for the icons.
    unix:icons.path = $$LOCAL_INSTALL_DIR/pixmaps
    unix:icons.files = ./Administrative/releaseFiles/kactus2.xpm ./Administrative/releaseFiles/kactus2.png

    # Files and destination path for the desktop shortcut.
    unix:desktop.path = ~/.local/share/applications
    unix:desktop.files = ./Administrative/releaseFiles/kactus2.desktop
}
