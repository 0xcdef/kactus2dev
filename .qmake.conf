# If empty, will be considered as an installation for all users, requiring admin-privileges.
LOCAL_INSTALL_DIR=""

isEmpty(LOCAL_INSTALL_DIR) {

    # Select paths for binaries in accordance with convention according to system bitness.
    bin_path = /usr/bin

    UNAME = $$system(uname -m)
    lib_path = /usr/lib
    equals(UNAME, x86_64): lib_path = /usr/lib64

    plugin_path = /usr/share/kactus2/plugins

    # Files and destination path for possible settings file upgrades.
    unix:upgrade.path = /usr/share/kactus2
    
    # Files and destination path for documentation files other than the manual.
    unix:doc.path = /usr/share/kactus2/doc
    
    # Files and destination path for the manual.
    unix:help.path = /usr/share/kactus2/help
    
    # Files and destination path for the IP-XACT files coming with an installation.
    unix:library.path = /usr/share/kactus2/library
    
    # Files and destination path for the icons.
    unix:icons.path = /usr/share/pixmaps
    
    # Files and destination path for the desktop shortcut.
    unix:desktop.path = /usr/share/applications

} else {
    # Binaries shall be located in the root of the target installation directory.
    bin_path = $$LOCAL_INSTALL_DIR
    lib_path = $$LOCAL_INSTALL_DIR
    plugin_path = $$LOCAL_INSTALL_DIR/Plugins

    # Files and destination path for possible settings file upgrades.
    unix:upgrade.path = $$LOCAL_INSTALL_DIR

    # Files and destination path for documentation files other than the manual.
    unix:doc.path = $$LOCAL_INSTALL_DIR

    # Files and destination path for the manual.
    unix:help.path = $$LOCAL_INSTALL_DIR/Help

    # Files and destination path for the IP-XACT files coming with an installation.
    unix:library.path = $$LOCAL_INSTALL_DIR/Library

    # Files and destination path for the icons.
    unix:icons.path = $$LOCAL_INSTALL_DIR/pixmaps

    # Files and destination path for the desktop shortcut.
    unix:desktop.path = ~/.local/share/applications
}

unix:upgrade.files = ./Administrative/releaseFiles/upgrade.cfg
unix:doc.files = ./Administrative/releaseFiles/*.txt
unix:help.files = ./Help/Kactus2Help.qch ./Help/Kactus2Help.qhc
unix:library.files = ./Administrative/releaseFiles/Library/*
unix:icons.files = ./Administrative/releaseFiles/kactus2.xpm ./Administrative/releaseFiles/kactus2.png
unix:desktop.files = ./Administrative/releaseFiles/kactus2.desktop

